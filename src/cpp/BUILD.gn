# Copyright 2017 The Procyon Authors
#
# Licensed under the Apache License, Version 2.0 (the "License");
# you may not use this file except in compliance with the License.
# You may obtain a copy of the License at
#
#     http://www.apache.org/licenses/LICENSE-2.0
#
# Unless required by applicable law or agreed to in writing, software
# distributed under the License is distributed on an "AS IS" BASIS,
# WITHOUT WARRANTIES OR CONDITIONS OF ANY KIND, either express or implied.
# See the License for the specific language governing permissions and
# limitations under the License.

static_library("procyon-cpp") {
  public = [
    "include/pn/arg",
    "include/pn/array",
    "include/pn/data",
    "include/pn/fwd",
    "include/pn/input",
    "include/pn/map",
    "include/pn/output",
    "include/pn/string",
    "include/pn/value",
  ]

  sources = [
    "src/array.cpp",
    "src/common.hpp",
    "src/data.cpp",
    "src/file.cpp",
    "src/map.cpp",
    "src/string.cpp",
    "src/value.cpp",
  ]

  public_configs = [ ":procyon_public" ]
  configs += [ ":procyon_private" ]

  public_deps = [
    "../c:procyon",
  ]
}

config("procyon_public") {
  include_dirs = [ "include" ]
  libs = [ "m" ]
}

config("procyon_private") {
  cflags = [
    "-Wall",
    "-Wextra",
    "-Wsign-compare",
    "-Werror",
    "-Wno-initializer-overrides",
  ]
}

executable("procyon-cpp-test") {
  testonly = true
  sources = [
    "test/data.test.cpp",
    "test/dump.test.cpp",
    "test/float.test.cpp",
    "test/format.test.cpp",
    "test/io.test.cpp",
    "test/lex.test.cpp",
    "test/matchers.cpp",
    "test/matchers.hpp",
    "test/parse.test.cpp",
    "test/string.test.cpp",
    "test/utf8.test.cpp",
    "test/value.test.cpp",
    "test/valuepp.test.cpp",
  ]
  if (target_os == "win") {
    output_extension = "exe"
  }
  deps = [
    ":procyon-cpp",
    "//ext/gmock:gmock_main",
  ]
  configs += [ ":procyon_private" ]
}
